% Lambda Q# grammar without polymorphism or arrays,
% but along with measurement command
% The LambdaQs.cf grammar should correspond to this file on completion.

% Written by Kartik Singhal and Sarah Marshall following Bob Harper's PFPL

metavar x {{ tex \textcolor{black}{x} }}, _                                                             ::= {{ com term variable }}
metavar q {{ tex \textcolor{YellowOrange}{q} }}, r {{ tex \textcolor{YellowOrange}{r} }}                ::= {{ com qubit symbols }}
metavar U {{ tex \textcolor{CarnationPink}{U}_{2^n} }}, V {{ tex \textcolor{CarnationPink}{V}_{2^n} }}  ::= {{ com unitary symbols }}
metavar l                                                                                               ::= {{ com label }}

indexvar i, j, n, n_a {{ tex n_a }}, n_b {{ tex n_b }} ::= {{ com indices }}

embed {{ tex-preamble
  \usepackage[mathx]{mathabx}

  \usepackage[dvipsnames]{xcolor}

  \usepackage{stackengine}
  \usepackage{scalerel}
  \stackMath
  \newcommand{\ccdot}{\scalebox{1.15}{$\SavedStyle\cdot$} }
  \newcommand{\altdiv}{\mathbin{\ThisStyle{%
    \stackunder[-.25\LMex]{\stackon[-.15\LMex]{\SavedStyle\sim}{\ccdot} }{\ccdot} } } }

  \newcommand{\igate}[1]{\ensuremath{\textcolor{CarnationPink}{\mathit{#1} } } }
}}

grammar

Typ, t {{ tex \textcolor{black}{\tau} }} :: '' ::= {{ com Types }}
  | qbit                                        :: M :: qubit
    {{ com \textcolor{RoyalBlue}{qbit}, opaque qubit type }}
  | qref [ q ]                                  ::   :: qubitRef
    {{ com \textcolor{RoyalBlue}{qref $[[q]]$}, qubit reference type }}
  | arr ( t1 ; t2 )                             ::   :: function
    {{ com $[[t1]]~\textcolor{RoyalBlue}{\rightarrow}~[[t2]]$ }}
  | cmd ( t )                                   ::   :: command
    {{ com \textcolor{RoyalBlue}{$[[t]]$ cmd} }}
  | prod ( </ li c-> ti // i IN 1 .. n /> )     ::   :: product
    {{ com \textcolor{RoyalBlue}{$\bigtimes_{l \in L}^n\,\tau_l$} }}
  | sum ( </ li c-> ti // , // i IN 1 .. n /> ) :: M :: sum
    {{ com \textcolor{RoyalBlue}{$\bigplus_{l \in L}\,\tau_l$} }}
  | bool                                        ::   :: bool
    {{ com \textcolor{RoyalBlue}{bool} }}
  | unit                                        ::   :: unit
    {{ com \textcolor{RoyalBlue}{unit} }}

Exp, e {{ tex \textcolor{black}{e} }} :: '' ::=  {{ com Expressions }}
  | x                                                      ::   :: variable
    {{ com \textcolor{RoyalBlue}{$[[x]]$} }}
  | let ( e1 ; x . e2 )                                    ::   :: definition
    (+ bind x in e2 +)
    {{ com \textcolor{RoyalBlue}{let}~$[[x]]$~\textcolor{RoyalBlue}{be}~$[[e1]]$~\textcolor{RoyalBlue}{in}~$[[e2]]$ }}
  | lam { t } ( x .  e )                                   ::   :: abstraction
    (+ bind x in e +)
    {{ com \textcolor{RoyalBlue}{$\lambda([[x]]:[[t]])[[e]]$} }}
  | ap ( e1 ; e2 )                                         ::   :: application
    {{ com $[[e1]]\textcolor{RoyalBlue}{(}[[e2]]\textcolor{RoyalBlue}{)}$ }}
  | cmd ( m )                                              ::   :: encapsulation
    {{ com \textcolor{RoyalBlue}{cmd $[[m]]$}, encapsulation }}
  | qloc [ q ]                                             ::   :: qubitLocation
    {{ com \textcolor{RoyalBlue}{\&$[[q]]$}, qubit location }}
  | tpl ( </ li c-> ei // , // i IN 1 .. n /> )            ::   :: tuple
    {{ com \textcolor{RoyalBlue}{$\langle e_l \rangle_{l \in L}$}, tuple }}
  | pr [ li ] ( e )                                        ::   :: projection
    {{ com \textcolor{RoyalBlue}{$[[e]] \cdot l$}, projection }}
  | in [ li ] { </ ti // , // i IN 1 .. n /> } ( e )       :: M :: injection
    {{ com \textcolor{RoyalBlue}{$l \cdot [[e]]$} }}
  % TODO: here xi's may need to bound in ei's
  | case ( e ; </ li c-> xi . ei // , // i IN 1 .. n /> )  :: M :: caseAnalysis
    {{ com \textcolor{RoyalBlue}{case $[[e]]\,\{ l \cdot x_l \hookrightarrow e_l \}_{l \in L}$} }}
  | true                                                   ::   :: true
    {{ com \textcolor{RoyalBlue}{true} }}
  | false                                                  ::   :: false
    {{ com \textcolor{RoyalBlue}{false} }}
  | if ( e ; e1 ; e2 )                                     ::   :: ite
    {{ com \textcolor{RoyalBlue}{if}~$[[e]]$~\textcolor{RoyalBlue}{then}~$[[e1]]$~\textcolor{RoyalBlue}{else}~$[[e2]]$ }}
  | not e                                                  ::   :: negation
    {{ com \textcolor{RoyalBlue}{$\neg~[[e]]$} }}
  | triv                                                   ::   :: trivial
    {{ com \textcolor{RoyalBlue}{$\langle \rangle$} }}
  | [ e1 / x ] e2                                          :: M :: subst
    {{ com substitution }}
  | ( e )                                                  :: M :: paren
    {{ com parentheses }}

Cmd, m {{ tex \textcolor{black}{m} }} :: '' ::= {{ com Commands }}
  | ret ( e )                       ::   :: return                             {{ com \textcolor{RoyalBlue}{ret $[[e]]$}, return }}
  | bnd ( e ; x . m )               ::   :: sequence       (+ bind x in m +)   {{ com \textcolor{RoyalBlue}{bnd $[[x]] \leftarrow [[e]] ; [[m]]$}, sequencing }}
  | newqref ( x . m )               ::   :: newqref        (+ bind x in m +)   {{ com \textcolor{RoyalBlue}{new $[[x]]$ in $[[m]]$}, new qubit reference }}
  | gateap [ U ] ( e )              ::   :: gateAppRef                         {{ com \textcolor{RoyalBlue}{$[[U]]$$([[e]])$}, gate application }}
  | diagap [ U , V ] ( e1 ; e2 )    ::   :: diagAppRef
    {{ com \textcolor{RoyalBlue}{D(\igate{U},\igate{V})}\textcolor{RoyalBlue}{(}$[[e1]]$\textcolor{RoyalBlue}{,} $[[e2]]$\textcolor{RoyalBlue}{)}, block diagonal }}
  | meas ( e )                      ::   :: measRef                            {{ com \textcolor{RoyalBlue}{meas$([[e]])$}, measure qbit }}
  | dcl ( q . m )                   :: M :: newQubit                           {{ com \textcolor{RoyalBlue}{dcl $[[q]]$ in $[[m]]$}, new (opaque) qubit }} % (+ bind q in m +)
  | gateapm [ U , q ]               :: M :: gateApp                            {{ com gate application (opaque) }}
  | measm [ q ]                     :: M :: meas                               {{ com measure qbit (opaque) }}
  | [ e / x ] m                     :: M :: substCmd                           {{ com substitution }}

Sugar, s :: '' ::= {{ com Derived forms }}
  | { x <- m1 ; m2 }    :: :: SeqComp
  | { m1 ; m2 }         :: :: SeqComp2
  | do e                :: :: Do
  | proc ( x : t ) m    :: :: Procedure
  | call e1 ( e2 )      :: :: Call
  | t1 => t2            :: :: Operation

G {{ tex \Gamma }} :: 'G_' ::= {{ com Typing context }}
  | empty                 ::  :: empty
  | G , x : t             ::  :: term

Sigma, sig {{ tex \Sigma }} :: 'S_' ::= {{ com Signature }}
  | empty               ::  :: empty
  | sig , q ~ qbit      ::  :: qbitsymbol

terminals :: 'terminals_' ::=
  | |-                  ::  :: turnstile  {{ tex \vdash }}            {{ com entails }}
  | |-->                ::  :: transition {{ tex \longmapsto }}       {{ com transition }}
  | c->                 ::  :: hrarrow    {{ tex \hookrightarrow }}   {{ com mapping }}
  | c.                  ::  :: cdot       {{ tex \cdot }}             {{ com projection }}
  | ~                   ::  :: sim        {{ tex \sim }}              {{ com tilde }}
  | ~.                  ::  :: dottilde   {{ tex \altdiv }}           {{ com dotted tilde }}
  | empty               ::  :: empty      {{ tex \varnothing }}       {{ com empty context }}
  | <=                  ::  :: leq        {{ tex \leq }}              {{ com less than or equal }}
  | <-                  ::  :: larrow     {{ tex \leftarrow }}
  | :=                  ::  :: define     {{ tex \triangleq }}        {{ com defined as }}
  | =>                  ::  :: darrow     {{ tex \Rightarrow }}       {{ com operation type }}

grammar
formula :: formula_ ::=
  | judgement            :: :: judgement
  | formula1 .. formulan :: :: dots
  | 1 <= i <= n          :: :: bounds

defns
Jdefined :: '' ::=
  defn
  s := user_syntax               :: :: deftypes :: '' {{ com Derived forms / syntactic sugar }} by

    ---------------------------------:: SeqComp
    {x<-m1; m2} := bnd(cmd(m1); x.m2)

    ------------------------------:: SeqCompU
    {m1; m2} := bnd(cmd(m1); _.m2)

    ------------------------:: Do
    do e := bnd(e; x.ret(x))

    --------------------------------:: Procedure
    proc (x:t) m := lam{t}(x.cmd(m))

    -----------------------------:: Call
    call e1(e2) := do (ap(e1;e2))

    ----------------------------:: OperationType
    t1 => t2 := arr(t1; cmd(t2))

defns
Jstatics :: '' ::=

  defn
  G |- e : t :: :: etyping :: ty_ {{ com Expression Typing }} by

    ----------------- :: Var
    G, x : t |- x : t

    G |- e1 : t1
    G, x : t1 |- e2 : t2
    ---------------------- :: Let
    G |- let(e1; x.e2) : t2

    G, x : t1 |- e : t2
    -------------------------------- :: Lam
    G |- lam{t1}(x.e) : arr(t1; t2)

    G |- e1 : arr(t2; t)
    G |- e2 : t2
    -------------------- :: Ap
    G |- ap(e1; e2) : t

    </ G |- ei : ti // i IN 1..n />
    --------------------------------------------------------------------------- :: Tpl
    G |- tpl(</ li c-> ei // i IN 1..n />) : prod(</ li c-> ti // i IN 1..n />)

    G |- e : prod(</ li c-> ti // i IN 1..n />)
    1 <= i <= n
    ------------------------------------------- :: Pr
    G |- pr[li](e) : ti

    G |- e : ti
    1 <= i <= n
    ------------------------------------------------------------------------- :: Inj
    G |- in[li]{</ ti // i IN 1..n />}(e) : sum(</ li c-> ti // i IN 1..n />)

    G |- e : sum(</ li c-> ti // i IN 1..n />)
    </ G, xi : ti |- ei : t // i IN 1..n />
    --------------------------------------------------- :: Case
    G |- case(e; </ li c-> xi . ei // i IN 1..n />) : t

  defn
  G |- sig e : t :: :: estyping :: tyS_ {{ com Expression Typing wrt Signature }} {{ tex [[G]] \vdash_{[[sig]]} [[e]] : [[t]] }} by

    G |- sig m ~. t
    ------------------------ :: Cmd
    G |- sig cmd(m) : cmd(t)

    --------------------------------- :: QLoc
    G |- sig,q~qbit qloc[q] : qref[q]

  defn
  G |- sig m ~. t :: :: cmdFormation :: cmd_ {{ com Well formed command w/ return type $[[t]]$ }} {{ tex [[G]] \vdash_{[[sig]]} [[m]]\ \altdiv\ [[t]] }} by

    G |- sig e : t
    -------------------- :: Ret
    G |- sig ret(e) ~. t

    G |- sig e : cmd(t)
    G, x:t |- sig m ~. t'
    ------------------------- :: Bnd
    G |- sig bnd(e;x.m) ~. t'

    G, x:qref[q] |- sig,q~qbit m ~. t
    --------------------------------- :: NewQRef
    G |- sig newqref(x.m) ~. t

    G |- sig e:prod ( </ li c-> qref [qi] // i IN 1 .. n /> )
    --------------------------------------------------------- :: GateApRef
    G |- sig gateap[U](e) ~. unit

    G |- sig e1:qref[q]
    G |- sig e2:prod ( </ li c-> qref [ri] // i IN 1 .. n /> )
    ---------------------------------------------------------- :: DiagApRef
    G |- sig diagap[U,V](e1;e2) ~. unit

    G |- sig e:qref[q]
    ------------------------ :: MeasRef
    G |- sig meas(e) ~. bool

    % Some final rules for qubit symbols (with resolved references) missing here

    G |- sig,q~qbit m ~. t
    ----------------------- :: Dcl
    G |- sig dcl(q.m) ~. t

    ------------------------------------ :: GateAp
    G |- sig,q~qbit gateapm[U,q] ~. unit

    -------------------------------- :: Meas
    G |- sig,q~qbit measm[q] ~. bool

defns
Jdynamics :: '' ::=

  defn
  e val :: :: values :: v_ {{ com Values }} by

    --------------- :: Lam
    lam{t}(x.e) val

    </ ei val // i IN 1..n />
    ------------------------------------- :: Tpl
    tpl(</ li c-> ei // i IN 1..n />) val

    e val
    -------------------------------------- :: Inj
    in[li]{</ ti // i IN 1 .. n />}(e) val

  defn
  e val sig :: :: svalues :: vS_ {{ com Values wrt Signature }} {{ tex [[e]]\ \textbf{val}_{[[sig]]} }} by

    -------------- :: Cmd
    cmd(m) val sig

    ---------------------- :: QLoc
    qloc[q] val sig,q~qbit

  defn
  e |--> e' :: :: trans :: tr_ {{ com Transition }} by

    e1 |--> e1'
    --------------------------------- :: Let
    let(e1; x.e2) |--> let(e1'; x.e2)

    e1 val
    --------------------------- :: LetInstr
    let(e1; x.e2) |--> [e1/x]e2

    e1 |--> e1'
    --------------------------- :: ApL
    ap(e1; e2) |--> ap(e1'; e2)

    e1 val
    e2 |--> e2'
    --------------------------- :: ApR
    ap(e1; e2) |--> ap(e1; e2')

    e2 val
    ----------------------------------- :: ApInstr
    ap(lam{t2}(x.e1); e2) |--> [e2/x]e1

    </ ei val // i IN 1..n_a />
    e |--> e'
    ------------------------------------------------------------------------------------------------------------------------------------------------------------------- :: Tpl
    tpl(</ li c-> ei // i IN 1..n_a />, l c-> e, </ lj' c-> ej' // j IN 1..n_b />) |--> tpl(</ li c-> ei // i IN 1..n_a />, l c-> e', </ lj' c-> ej' // j IN 1..n_b />)

    e |--> e'
    ------------------------- :: Pr
    pr[li](e) |--> pr[li](e')

    tpl(</ li c-> ei // i IN 1..n />) val
    1 <= j <= n
    ------------------------------------------------- :: PrInstr
    pr[lj](tpl(</ li c-> ei // i IN 1..n />)) |--> ej

    e |--> e'
    ----------------------------------------------------------------------- :: Inj
    in[li]{</ ti // i IN 1..n />}(e) |--> in[li]{</ ti // i IN 1..n />}(e')

    e |--> e'
    --------------------------------------------------------------------------------------- :: Case
    case(e; </ li c-> xi.ei // i IN 1..n />) |--> case(e'; </ li c-> xi.ei // i IN 1..n />)

    in[lj]{</ ti // i IN 1..n />}(e) val
    1 <= j <= n
    ------------------------------------------------------------------------------------- :: CaseInstr
    case(in[lj]{</ ti // i IN 1..n />}(e); </ li c-> xi.ei // i IN 1..n />) |--> [e/xj]ej

  defn
  e |--> sig e' :: :: Strans :: trS_ {{ com Transition wrt Signature }} {{ tex [[e]] \underset{[[sig]]}{\longmapsto} [[e']] }} by

  defn
  m final sig :: :: final :: fn_ {{ com State $[[m]]$ is complete }} {{ tex [[m]]\ \textbf{final}_{[[sig]]} }} by

    e val sig
    ---------------- :: Ret
    ret(e) final sig

  defn
  m |--> sig m' :: :: Stransmu :: st_ {{ com State transition }} {{ tex [[m]] \underset{[[sig]]}{\longmapsto} [[m']] }} by

    e |--> sig e'
    ----------------------- :: Ret
    ret(e) |--> sig ret(e')

    e |--> sig e'
    ------------------------------- :: Bnd
    bnd(e;x.m) |--> sig bnd(e';x.m)

    e val sig
    ------------------------------------ :: BndInstr
    bnd(cmd(ret(e));x.m) |--> sig [e/x]m

    m1 |--> sig m1'
    --------------------------------------------- :: BndCmd
    bnd(cmd(m1);x.m2) |--> sig bnd(cmd(m1');x.m2)

    m |--> sig,q~qbit m'
    ----------------------------------- :: NewQRef
    newqref(x.m) |--> sig newqref(x.m')

    e val sig
    --------------------------------- :: NewQRefInstr
    newqref(x.ret(e)) |--> sig ret(e)

    e |--> sig e'
    ----------------------------------------------- :: GateApRef
    gateap[U](e) |--> sig gateap[U](e')

    e1 |--> sig e1'
    ----------------------------------------------- :: DiagApRefL
    diagap[U,V](e1;e2) |--> sig diagap[U,V](e1';e2)

    e1 val sig
    e2 |--> sig e2'
    ----------------------------------------------- :: DiagApRefR
    diagap[U,V](e1;e2) |--> sig diagap[U,V](e1;e2')

    e |--> sig e'
    --------------------------------------- :: MeasRef
    meas(e) |--> sig meas(e')

    % Some final rules for qubit symbols (with resolved references) missing here

    e val sig,q~qbit
    ----------------------------- :: Dcl
    dcl(q.ret(e)) |--> sig ret(e)

    ----------------------------------------------- :: GateApRefInstr
    gateap[U](qloc[q]) |--> sig,q~qbit gateapm[U,q]


    -------------------------------------- :: MeasInstr
    meas(qloc[q]) |--> sig,q~qbit measm[q]
