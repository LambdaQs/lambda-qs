entrypoints Exp;

-- Metavariables
MVar. Var ::= Ident; -- variables
MUni. Uni ::= Ident; -- unitaries
MKey. Key ::= Ident; -- qubit symbols/unique keys

-- Types
-- TQbit. Typ ::= "qbit";                      -- opaque qubit type
TQref. Typ ::= "qref" "[" Key "]";          -- qubit reference
TParr. Typ ::= "parr" "(" Typ "->" Typ ")"; -- partial function
TCmd.  Typ ::= "cmd" "(" Typ ")";           -- command
TNat.  Typ ::= "nat";
TBool. Typ ::= "bool";
TUnit. Typ ::= "unit";

-- Expressions
EVar.  Exp ::= Var;
ENat.  Exp ::= Integer;
EPlus. Exp ::= Exp "+" Exp;
ETriv. Exp ::= "()";
EQloc. Exp ::= "&" Key;                       -- qubit locations
ELet.  Exp ::= "let" Var "be" Exp "in" Exp;   -- let expression
ELam.  Exp ::= "\\" "(" Var ":" Typ ")" Exp;  -- lambda abstraction
EAp.   Exp ::= Exp "(" Exp ")";               -- application
ECmd.  Exp ::= "cmd" Cmd;                     -- encapsulation

-- Commands
CRet.   Cmd ::= "ret" Exp;                                -- return
CBnd.   Cmd ::= "bnd" Var "<-" Exp ";" Cmd;               -- sequence
CNew.   Cmd ::= "new" Var "in" Cmd;                       -- new qubit reference
CGap.   Cmd ::= Uni "(" Exp ")";                          -- unitary gate application
CDiag.  Cmd ::= "D" "(" Uni "," Uni ";" Exp "," Exp ")";  -- diagonal gate application
CMeas.  Cmd ::= "meas" "(" Exp ")";                       -- measurement
-- CDcl.   Cmd ::= "dcl" Key "in" Cmd;                    -- qubit symbol creation
-- CCap.   Cmd ::= "Controlled" Uni "(" Exp "," Exp ")";
-- CGet.   Cmd ::= "get" "[" Key "]";
-- CSet.   Cmd ::= "set" "[" Key "]" "(" Exp ")";

-- Programming idioms

-- sequential composition of commands
seq. Cmd ::= "{" Var "<-" Cmd ";" Cmd "}";
define seq x m1 m2 = CBnd x (ECmd m1) m2;

-- `do e` executes an encapsulated command and returns its result
-- This encoding requires an explicit variable argument
-- Example: do 1 x
do. Exp ::= "do" Exp Var;
define do e x = ECmd (CBnd x e (CRet (EVar x)));

-- procedure
proc. Exp ::= "proc" "(" Var ":" Typ ")" Cmd;
define proc x t m = ELam x t (ECmd m);

-- procedure call
-- This encoding also requires an explicit variable argument
pcall. Exp ::= "call" Exp "(" Exp ")" Var;
define pcall e1 e2 x = ECmd (CBnd x (EAp e1 e2) (CRet (EVar x)));;

internal EVarT. Exp ::= "(" Var ":" Typ ")";

{-

  # Build and run using:

      ```
      ## for Haskell
      bnfc -d -m LambdaQs.cf  &&  make
      ./LambdaQs/Test

      ## for OCaml with Menhir
      bnfc -m --ocaml-menhir LambdaQs.cf  &&  make
      ./TestLambdaQs
      ```
-}
